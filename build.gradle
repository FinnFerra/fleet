plugins {
    id 'java'
    id 'com.eriwen.gradle.js' version '2.14.1'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

version = '1.0.0'

dependencies {

    // Logging
    runtime 'org.apache.logging.log4j:log4j-api:2.11.1'
    runtime 'org.apache.logging.log4j:log4j-core:2.11.1'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.11.1'

    // HTTP Framework
    compile 'com.sparkjava:spark-core:2.7.2'
    compile 'com.sparkjava:spark-template-freemarker:2.7.1'
    compile 'org.apache.httpcomponents:httpclient:4.5.7'

    // JSON Mapping/Marshalling
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.6'
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.6'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.6'

    // Database
    compile 'com.zaxxer:HikariCP:3.3.1'
    compile 'org.mariadb.jdbc:mariadb-java-client:2.4.0'
    compile 'org.flywaydb:flyway-core:6.0.0-beta'

    // MISC
    compile 'org.apache.commons:commons-lang3:3.7'


    // Unit Testing
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-all:1.10.19'
}

jar {

    manifest {
        attributes "Main-Class": "io.linuxserver.fleet.core.Main"
    }

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

javascript.source {

    prod {
        js {
            srcDir 'src/main/resources/assets/js'
            include '*.js'
        }
    }
}

combineJs {

    encoding = "UTF-8"
    source = javascript.source.prod.js.files
    dest = file('src/main/resources/assets/js/all.js')
}

minifyJs {

    source = combineJs
    dest = file('src/main/resources/assets/js/all.min.js')
}